###############################################################################
# Configure rsyslog daemon for special CFEngine logging, and ensure the
# service stays running.
###############################################################################

bundle agent a16_cfe_rsyslog
{

meta:

    "tags" slist => { "autorun" };

methods:

    gc_agent_ready::
    "a16rsys" usebundle => a16_cfe_rsyslog_run;

    "a16rsmy" usebundle => a16_cfe_rsyslog_mysql_run;

}

bundle agent a16_cfe_rsyslog_run
{

vars:

    "s_dir" string  => "$(gv.text)";

    "s_conf" string => "rsyslog.d.conf.txt";

files:

    "/etc/rsyslog.d/03-cfengine.conf"
    handle          => "a16h_rsyslog_conf",
    create          => "true",
    perms           => mog("644","root","root"),
    copy_from       => local_dcp("$(s_dir)/$(s_conf)"),
    classes         => if_repaired("a16c_rsyslog_fixed");

processes:

    "$(gv.p[rsyslog])"
    restart_class => "a16c_rsyslog_dead";

commands:

    a16c_rsyslog_fixed::
    "$(gv.restart[rsyslog])"
    classes     => if_repaired("a16c_rsyslog_restarted");

    a16c_rsyslog_dead::
    "$(gv.start[rsyslog])";

    "$(gv.enable[rsyslog])"
    classes     => if_repaired("a16c_rsyslog_started");

    a16c_rsyslog_restarted::
    "$(gv.log_notice)"
    args => "Fixed config and restarted rsyslogd";

    a16c_rsyslog_started::
    "$(gv.log_warning)"
    args => "Started rsyslogd and told it to autostart";

}

bundle agent a16_cfe_rsyslog_mysql_run
{

# This bundle enables CFEngine logging to a MySQL/MariaDB. It relies on
# the rsyslog-mysql package, and on the presence of a credentials
# file. (Note that the credentials file must be created on the
# CFEngine hub at your site. It is ignored by git, because it would
# contain private URI and authentication info.

vars:

    "s_secdir" string  => "$(gv.secrets)";

    "s_mdir" string  => "$(gv.mustache)";

    "s_json" string => "mysql-creds.json";

    "s_conf" string => "rsyslog-mysql.conf.mustache";

classes:

    "a16c_pkg_ready" expression => fileexists("/lib64/rsyslog/ommysql.so");

    "a16c_creds_ready" expression => fileexists("$(s_secdir)/$(s_json)");

files:

    a16c_pkg_ready.a16c_creds_ready::
    "/etc/rsyslog.d/04-mysql.conf"
    handle          => "a16h_rsyslog_mysql_conf",
    create          => "true",
    perms           => mog("600","root","root"),
    edit_template   => "$(s_mdir)/$(s_conf)",
    template_method => "mustache",
    template_data   => readjson("$(s_secdir)/$(s_json)", 50000),
    classes         => if_repaired("a16c_rsyslog_mysql_fixed");

commands:

    a16c_rsyslog_mysql_fixed::
    "$(gv.restart[rsyslog])"
    classes     => if_repaired("a16c_rsyslog_restarted");

    a16c_rsyslog_restarted::
    "$(gv.log_notice)"
    args => "Fixed config and restarted rsyslogd";

}
