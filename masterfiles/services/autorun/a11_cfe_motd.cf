###############################################################################
# Install an motd that reports CFEngine version and hub.
###############################################################################

bundle agent a11_cfe_motd
{

meta:

    "tags" slist => { "autorun" };

methods:

    gc_agent_ready::
    "a11motd" usebundle => a11_cfe_motd_run;

}

bundle agent a11_cfe_motd_run
{

vars:

    "s_dir" string  => "$(gv.mustache)";

    "s_conf" string => "motd.mustache";

classes:

    "a11c_update_motd_installed" expression
            => fileexists("/var/lib/update-motd/.");

files:

    "/etc/motd"
    handle          => "a11h_motd",
    create          => "true",
    perms           => mog("644","root","root"),
    edit_template   => "$(s_dir)/$(s_conf)",
    template_method => "mustache",
    template_data   => parsejson('{
                           "cfe_version": "$(sys.cf_version)",
                           "cfe_hub": "$(sys.policy_hub)"
                       }'),
    classes         => if_repaired("a11c_fixed");

    a11c_update_motd_installed::
    "/var/lib/update-motd/disabled"
    handle          => "a11h_update_motd_disable",
    create          => "true",
    perms           => mog("644","root","root"),
    classes         => if_repaired("a11c_disabled_update_motd");

commands:

    a11c_fixed::
    "$(gv.log_notice)"
    args => "Installed motd with CFEngine version and hub info";

    a11c_disabled_update_motd::
    "$(gv.log_notice)"
    args => "Disabled the update-motd (Amazon package) process";

}
