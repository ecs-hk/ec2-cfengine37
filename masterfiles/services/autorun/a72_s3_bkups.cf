###############################################################################
# Install a dailly cronjob to push encrypted backups to AWS S3.
###############################################################################

bundle agent a72_s3_bkups
{

meta:

    "tags" slist => { "autorun" };

methods:

    gc_supported_os::
    "a72s3bkup" usebundle => a72_s3_bkup_script_run;

    "a72s3key" usebundle => a72_s3_key_run;

    "a72s3creds" usebundle => a72_s3_creds_run;

}

bundle agent a72_s3_bkup_script_run
{

vars:

    "s_dir" string  => "$(gv.scripts)";

    "s_name" string => "bkup-to-s3";

files:

    "/etc/cron.daily/80-$(s_name)"
    handle          => "a72h_bkup_cronjob",
    create          => "true",
    perms           => mog("755","root","root"),
    copy_from       => local_dcp("$(s_dir)/$(s_name).txt"),
    classes         => if_repaired("a72c_bkup_script_fixed");

commands:

    a72c_bkup_script_fixed::
    "$(gv.log_notice)"
    args => "Installed cronjob for S3 backups";

}

bundle agent a72_s3_key_run
{

vars:

    "s_dir" string  => "$(gv.secrets)";

    "s_name" string => "bkup-to-s3.key";

classes:

    "a72c_keyfile_ready" expression => fileexists("$(s_dir)/$(s_name).txt");

files:

    a72c_keyfile_ready::
    "/usr/local/etc/$(s_name)"
    handle          => "a72h_bkup_key",
    create          => "true",
    perms           => mog("400","root","root"),
    copy_from       => local_dcp("$(s_dir)/$(s_name).txt"),
    classes         => if_repaired("a72c_bkup_key_fixed");

commands:

    a72c_bkup_key_fixed::
    "$(gv.log_notice)"
    args => "Installed encryption key for S3 backups";

}

bundle agent a72_s3_creds_run
{

# This bundle runs files promises that have a similar effect as
# the shell command-line invocation of 'aws configure' (using the
# AWS CLI program). It relies on the presence of a credentials file.

vars:

    "s_secdir" string  => "$(gv.secrets)";

    "s_mdir" string  => "$(gv.mustache)";

    "s_json" string => "bkup-to-s3-creds.json";

    "s_conf" string => "aws-credentials.mustache";

classes:

    "a72c_creds_ready" expression => fileexists("$(s_secdir)/$(s_json)");

files:

    a72c_creds_ready::
    "/root/.aws/."
    handle          => "a72h_aws_confdir",
    create          => "true",
    perms           => mog("755","root","root"),
    classes         => if_repaired("a72c_aws_conf_fixed");

    "/root/.aws/credentials"
    handle          => "a72h_aws_creds",
    depends_on      => { "a72h_aws_confdir" },
    create          => "true",
    perms           => mog("600","root","root"),
    edit_template   => "$(s_mdir)/$(s_conf)",
    template_method => "mustache",
    template_data   => readjson("$(s_secdir)/$(s_json)", 50000),
    classes         => if_repaired("a72c_aws_conf_fixed");

commands:

    a72c_aws_conf_fixed::
    "$(gv.log_notice)"
    args => "Fixed AWS CLI config for root";

}
