###############################################################################
# Various CFEngine housekeeping tasks:
# - Delete files that were (but are no longer) CFE managed.
# - Clean up leftover CFE-generated cruft following edits by promises.
# - Maintain usefule sysadmin symlinks related to CFE.
###############################################################################

bundle agent a10_cfe_housekeeping
{

meta:

    "tags" slist => { "autorun" };

methods:

    gc_agent_ready::
    "a10rmunmanaged" usebundle => a10_cfe_rm_unmanaged;
    "a10rmcruft" usebundle => a10_cfe_rm_cruft;
    "a10symlink" usebundle => a10_cfe_symlink;

}

bundle agent a10_cfe_rm_unmanaged
{

vars:

    "sfiles" slist  => {
        "/usr/local/etc/bkup-to-s3.env",
        "/usr/local/etc/bkup-to-s3.list"
        };

files:

    "$(sfiles)"
    delete => tidy;

}

bundle agent a10_cfe_rm_cruft
{

files:

    "/etc/.*\.(cfsaved|cf-before-edit)"
    delete => tidy;

    "/etc/.*/.*\.(cfsaved|cf-before-edit)"
    delete => tidy;

    "/usr/local/(etc|bin|sbin)/.*\.(cfsaved|cf-before-edit)"
    delete => tidy;

}

bundle agent a10_cfe_symlink
{

files:

    am_policy_hub::
    "/cfe-master/."
    link_from         => ln_s("$(sys.masterdir)/services/autorun/."),
    move_obstructions => "true";

}
