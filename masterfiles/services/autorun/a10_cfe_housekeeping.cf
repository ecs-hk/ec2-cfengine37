###############################################################################
# Clean up any CFEngine-generated files from directories that could cause
# unpleasant side-effects.
###############################################################################

bundle agent a10_cfe_housekeeping
{

meta:

    "tags" slist => { "autorun" };

methods:

    gc_agent_ready::
    "a10housekeep" usebundle => a10_cfe_housekeeping_run;

}

bundle agent a10_cfe_housekeeping_run
{

vars:

    # Pretty silly to hardcode all this, but the {baz,boo} globbing
    # does not appear to be working correctly.
    "glob01" string => "/etc/cron.*/*.cfsaved";
    "glob02" string => "/etc/cron.*/*.cf-before-edit";
    "glob03" string => "/etc/logrotate.d/*.cfsaved";
    "glob04" string => "/etc/logrotate.d/*.cf-before-edit";
    "glob05" string => "/etc/rsyslog.d/*.cfsaved";
    "glob06" string => "/etc/rsyslog.d/*.cf-before-edit";
    "glob07" string => "/etc/yum.repos.d/*.cfsaved";
    "glob08" string => "/etc/yum.repos.d/*.cf-before-edit";
    "glob09" string => "/usr/local/etc/*.cfsaved";
    "glob10" string => "/usr/local/etc/*.cf-before-edit";
    "glob11" string => "/usr/local/bin/*.cfsaved";
    "glob12" string => "/usr/local/bin/*.cf-before-edit";
    
    # And filefiles() appears to want a list of strings rather than
    # an slist.
    "cruft" slist  => findfiles("$(glob01)","$(glob02)","$(glob03)",
                                "$(glob04)","$(glob05)","$(glob06)",
                                "$(glob07)","$(glob08)","$(glob09)",
                                "$(glob10)","$(glob11)","$(glob12)"
                               	);

files:

    "$(cruft)"
    delete  => tidy,
    classes => if_repaired("a10c_deleted_files");

commands:

    a10c_deleted_files::
    "$(gv.log_notice)"
    args => "Deleted CFE-generated duplicates from various directories";

}
