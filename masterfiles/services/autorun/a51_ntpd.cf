###############################################################################
# Configure ntp service and ensure it's running.
###############################################################################

bundle agent a51_ntpd
{

meta:

    "tags" slist => { "autorun" };

methods:

    amzn_2015_09::
    "a51ntpd" usebundle => a51_ntpd_run;

}

bundle agent a51_ntpd_run
{

vars:

    "s_dir" string  => "$(a05_vars.text)";

    "s_conf" string => "ntp.conf.txt";

files:

    "/etc/ntp.conf"
    handle          => "a51h_ntpd_conf",
    create          => "false",
    perms           => mog("644","root","root"),
    copy_from       => local_dcp("$(s_dir)/$(s_conf)"),
    classes         => if_repaired("a51c_ntpd_fixed");

processes:

    "\bntpd\b"
    restart_class => "a51c_ntpd_dead";

commands:

    a51c_ntpd_fixed::
    "/etc/init.d/ntpd restart"
    classes       => if_repaired("a51c_ntpd_restarted");

    a51c_ntpd_dead::
    "/etc/init.d/ntpd start";

    "/sbin/chkconfig ntpd on"
    classes     => if_repaired("a51c_ntpd_started");

    a51c_ntpd_restarted::
    "$(a05_vars.log_notice)"
    args => "Fixed config and restarted ntpd";

    a51c_ntpd_started::
    "$(a05_vars.log_warning)"
    args => "Started ntpd and told it to autostart";

}
