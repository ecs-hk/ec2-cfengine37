###############################################################################
# Configure ntp service and ensure it's running.
###############################################################################

bundle agent a51_ntpd
{

meta:

    "tags" slist => { "autorun" };

methods:

    gc_agent_ready::
    "a51ntpd" usebundle => a51_ntpd_run;

}

bundle agent a51_ntpd_run
{

vars:

    "s_dir" string  => "$(gv.text)";

    "s_conf" string => "ntp.conf.txt";

files:

    "/etc/ntp.conf"
    handle          => "a51h_ntpd_conf",
    create          => "false",
    perms           => mog("644","root","root"),
    copy_from       => local_dcp("$(s_dir)/$(s_conf)"),
    classes         => if_repaired("a51c_ntpd_fixed");

processes:

    "$(gv.p[ntpd])"
    restart_class => "a51c_ntpd_dead";

commands:

    a51c_ntpd_fixed::
    "$(gv.restart[ntpd])"
    classes       => if_repaired("a51c_ntpd_restarted");

    a51c_ntpd_dead::
    "$(gv.start[ntpd])";

    "$(gv.enable[ntpd])"
    classes     => if_repaired("a51c_ntpd_started");

    a51c_ntpd_restarted::
    "$(gv.log_notice)"
    args => "Fixed config and restarted ntpd";

    a51c_ntpd_started::
    "$(gv.log_warning)"
    args => "Started ntpd and told it to autostart";

}
